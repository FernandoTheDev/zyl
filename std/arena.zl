import std.libc.mem : { malloc, free, realloc, memset }

struct Arena {
    char* base = null
    int size = 0
    int offset = 0

    void create(Arena* self, int size) {
        self.base = malloc(size)
        self.size = size
    }

    void* alloc(Arena* self, int amount) {
        int __sum = self.offset + amount
        if __sum > self.size {
            int nsize = self.size * 2
            if nsize < __sum
                nsize = __sum
            char* bff = (char*) realloc(self.base, nsize)
            if bff == null
                return null
            self.base = bff
            self.size = nsize
        }
        void* ptr = self.base + self.offset
        self.offset = self.offset + amount
        return ptr
    }

    void* calloc(Arena* self, int amount) {
        void* ptr = self.alloc(amount)
        if ptr != null memset(ptr, 0, amount)
        return ptr
    }

    void reset(Arena* self) {
        self.offset = 0
    }

    void destroy(Arena* self) {
        if self.base != null free(self.base)
        self.base = null
        self.offset = 0
        self.size = 0
    }
}
