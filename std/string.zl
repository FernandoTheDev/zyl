import std.libc.mem : { malloc, memcpy, free }
import std.libc.string : { strlen }
import std.libc.io : { printf, exit }
import std.arena

struct String {
    string str = ""
    Arena* arena = null;
    bool owns_memory = false

    void concat(String* self, string str2) {
        int l1 = strlen(self.str)
        int l2 = strlen(str2)
        
        string out;
        
        if self.arena == null
            out = (string) malloc(l1 + l2 + 1)
        else
            out = (string) self.arena.alloc(l1 + l2 + 1)
        
        if out == null
            self.error("Allocation failed in String.concat") 

        memcpy(out, self.str, l1)
        memcpy(out + l1, str2, l2 + 1) // Copia str2 + \0

        if self.owns_memory
            free(self.str)

        self.str = out
        if self.arena == null
            self.owns_memory = true
    }

    void destroy(String* self) {
        if self.owns_memory {
            free(self.str)
            self.str = ""
            self.owns_memory = false
        }
    }

    void opAddAssign(String* self, string left) { self.concat(left) }
    void opAddAssign(String* self, char left) {
        int l1 = strlen(self.str)

        string out;

        if self.arena == null
            out = (string) malloc(l1 + 2)  // +1 para o char, +1 para \0
        else
            out = (string) self.arena.alloc(l1 + 2)

        if out == null
            self.error("Allocation failed in String.opAddAssign(char)")

        memcpy(out, self.str, l1)
        out[l1] = left
        out[l1 + 1] = '\0'

        if self.owns_memory
            free(self.str)

        self.str = out
        if self.arena == null
            self.owns_memory = true
    }

    int length(String* self) {
        return strlen(self.str)
    }

    void clear(String* self) {
        self.str = ""
    }

    void error(String* self, string msg) {
        printf("Error: %s\n", msg)
        exit(1)
    }
}

string charToStr(char c, Arena* arena) {
    char* str = (char*) arena.alloc(2)
    str[0] = c
    str[1] = '\0'
    return str
}
